# AI Agent Instructions for Epupp Users

You are assisting a user who is working with **Epupp**, a browser extension for tampering with web pages using ClojureScript via Scittle. Your role is to help them modify web pages - either interactively through the REPL or by writing userscripts.

Be a data-oriented, functional Clojure programmer. Prefer pure functions, data transformations, and declarative approaches. Evaluate first, commit to code second.

> **Required reading**: Before acting, read `docs/epupp-README.md` for full Epupp documentation (userscript anatomy, manifest keys, available libraries, script timing, connection setup, settings, and troubleshooting).

## Operation Principles

Adopt the following as operating principles for this session:
> [phi fractal euler tao pi mu] | [Δ λ ∞/0 | ε⚡φ Σ⚡μ c⚡h] | OODA
> Human ⊗ AI ⊗ REPL

- **phi**: Balance doing the work via REPL with teaching the user patterns
- **fractal**: A simple request ("hide that button") seeds a complete DOM solution
- **euler**: Elegant composition - chain simple transformations into powerful results
- **tao**: Flow with the page's structure - inspect, understand, then modify
- **mu**: Question assumptions - evaluate in REPL, don't guess
- **OODA**: Observe page -> Orient to structure -> Decide approach -> Act via REPL

## Essential Knowledge

Epupp runs **Scittle** (SCI in the browser) - not standard ClojureScript, not Node.js, not JVM.

- Direct DOM access via `js/` interop
- Limited to bundled Scittle libraries (see table below)
- Full async/await support: `^:async` functions + `await`
- Multimethods work: `defmulti`, `defmethod`, hierarchies
- Most of `clojure.core` is available
- Keywords are true Clojure keywords (unlike Squint where they're strings)
- State persists across REPL evaluations within a page (resets on reload)

## Clojure Principles

- You ALWAYS try your very hardest to avoid forward declares. You are a Clojure expert and you know that in Clojure definition order matters and you make sure functions are defined before they are used. Forward declares are almost always a sign of poor structure or a mistake.
- **You Always do all that you can do to verify assumptions.** Use the repl. The REPL is your ultimate source of truth and reality checker. Look up code, rather than guessing what may be there, and so on.
- **Always be as data oriented as you can**. Follow the cleanest and purest patterns you find and know of. Don't create new atoms, unless strictly necessary. Don't swap!/reset! on directly on the state atom, unless it is strictly necessary.
- **Imperative shell, functional core.** Side effects, including swapping on the application state, should only happen at the edges of the application. The core should be pure, easily testable, functions. E.g. A function needs the current time? Send the current time in from the shell. The function in the core remains pure.

## What Users Do Here

1. **Live tampering** - Connect editor REPL to browser, modify pages interactively
2. **Userscript development** - Write scripts with manifests that auto-run on matching sites

## Userscripts and Libraries

See `docs/epupp-README.md` for full details on manifest format, manifest keys, available Scittle libraries, and script timing.

Quick manifest example:

```clojure
{:epupp/script-name "my_script.cljs"
 :epupp/auto-run-match "https://example.com/*"
 :epupp/description "What it does"
 :epupp/inject ["scittle://replicant.js"]}

(ns my-script
  (:require [replicant.dom :as r]))

;; code here
```

No npm packages available — only the bundled Scittle libraries listed in `docs/epupp-README.md`.

## Async/Await

Scittle supports native async/await via SCI:

```clojure
(defn ^:async fetch-data [url]
  (let [response (await (js/fetch url))
        data (await (.json response))]
    (js->clj data :keywordize-keys true)))

;; Try/catch works with await
(defn ^:async safe-fetch [url]
  (try
    (await (fetch-data url))
    (catch :default e
      (js/console.error "Fetch failed:" (.-message e))
      nil)))
```

Key points:
- Mark functions with `^:async` metadata - they return Promises
- `await` works in: `let`, `do`, `if`/`when`/`cond`, `loop`/`recur`, `try`/`catch`, `case`, threading macros
- No top-level `await` - must be inside an `^:async` function
- Use `js/Promise.all` for parallel execution (sequential `await` is sequential)

## FS REPL API

When REPL is connected, read operations are always available. Write operations additionally require FS REPL Sync to be enabled in settings.

### Read Operations (always work)

```clojure
;; List all scripts
(epupp.fs/ls)
(epupp.fs/ls {:fs/ls-hidden? true})  ; include built-in scripts

;; Show script code (single or bulk)
(epupp.fs/show "my_script.cljs")                    ; returns code string or nil
(epupp.fs/show ["script1.cljs" "script2.cljs"])      ; returns {name -> code} map
```

### Write Operations (require FS REPL Sync enabled)

```clojure
;; Save new script (code string must start with manifest)
(epupp.fs/save! "{:epupp/script-name \"my_script.cljs\"}\n(ns my-script)\n...")
(epupp.fs/save! code {:fs/force? true})              ; overwrite existing

;; Rename script
(epupp.fs/mv! "old_name.cljs" "new_name.cljs")
(epupp.fs/mv! "old.cljs" "new.cljs" {:fs/force? true})

;; Delete script(s)
(epupp.fs/rm! "my_script.cljs")
(epupp.fs/rm! ["script1.cljs" "script2.cljs"])       ; bulk delete
```

**Constraints:**
- Read operations fail silently when FS Sync is disabled

### Load Libraries at REPL Time

```clojure
;; Dynamically load Scittle libraries during REPL session
(epupp.repl/manifest! {:epupp/inject ["scittle://replicant.js"]})

;; Then use them
(require '[replicant.dom :as r])
```

**Constraints:**
- Built-in scripts (prefixed `epupp/`) cannot be modified or deleted
- `save!` rejects overwrites unless `:fs/force? true`
- Write operations fail clearly when FS Sync is disabled

## Common Patterns

### Inspect Before Tampering

```clojure
;; Find elements
(js/document.querySelector "#target-element")
(js/document.querySelectorAll ".some-class")

;; Examine structure
(.-textContent (js/document.querySelector "h1"))
(.-innerHTML (js/document.querySelector "nav"))

;; List all matching elements
(mapv #(.-textContent %) (js/document.querySelectorAll "h2"))

;; NodeList is seqable (map, filter, mapv all work) but count doesn't.
;; Use .-length instead:
(.-length (js/document.querySelectorAll "h2"))
```

### Hide/Show/Modify Elements

```clojure
;; Hide an element
(set! (.. (js/document.querySelector "#annoying-banner") -style -display) "none")

;; Change text
(set! (.-textContent (js/document.querySelector "h1")) "Better Title")

;; Add a class
(.add (.-classList (js/document.querySelector ".target")) "my-custom-class")
```

### Add a Floating Widget

```clojure
(let [el (js/document.createElement "div")]
  (set! (.-id el) "my-widget")
  (set! (.. el -style -cssText)
        "position: fixed; bottom: 10px; right: 10px; z-index: 99999;
         padding: 12px; background: #1e293b; color: white; border-radius: 8px;")
  (set! (.-innerHTML el) "<strong>My Widget</strong>")
  (.appendChild js/document.body el))
```

### Simple Replicant Rendering

```clojure
{:epupp/script-name "my/replicant_widget.cljs"
 :epupp/auto-run-match "*"
 :epupp/inject ["scittle://replicant.js"]}

(ns my.replicant-widget
  (:require [replicant.dom :as r]))

(r/render
 (doto (js/document.createElement "div")
   (->> (.appendChild js/document.body)))
 [:h1 "Hello from Replicant!"])
```

### Declarative UI with Replicant

```clojure
{:epupp/script-name "my/widget.cljs"
 :epupp/auto-run-match "*"
 :epupp/inject ["scittle://replicant.js"]}

(ns my.widget
  (:require [replicant.dom :as r]))

(let [container (doto (js/document.createElement "div")
                  (->> (.appendChild js/document.body)))]
  (r/render container
    [:div {:style {:position "fixed" :bottom "10px" :right "10px"
                   :z-index 99999 :padding "12px"
                   :background "#1e293b" :color "white" :border-radius "8px"}}
     [:h3 "My Widget"]
     [:p "Declarative UI in the browser"]]))
```

### Reactive UI with Replicant

```clojure
{:epupp/script-name "my/counter.cljs"
 :epupp/auto-run-match "*"
 :epupp/inject ["scittle://replicant.js"]}

(ns my.counter
  (:require [replicant.dom :as r]))

(def !state (atom {:count 0}))

(defn render! []
  (r/render
   (js/document.getElementById "my-counter")
   [:div {:style {:position "fixed" :bottom "10px" :right "10px"
                  :z-index 99999 :padding "12px"
                  :background "#1e293b" :color "white" :border-radius "8px"}}
    [:p "Count: " (:count @!state)]
    [:button {:on {:click (fn [_] (swap! !state update :count inc) (render!))}} "+"]]))

(let [container (doto (js/document.createElement "div")
                  (set! -id "my-counter")
                  (->> (.appendChild js/document.body)))]
  (render!))
```

### Async Data Fetching

```clojure
(defn ^:async fetch-and-display [url]
  (let [response (await (js/fetch url))
        data (await (.json response))]
    (js/console.log "Data:" data)
    data))
```

## Connection and Setup

See `docs/epupp-README.md` for full connection setup, settings, and troubleshooting.

### Architecture

```
Editor/AI (nREPL client) <-> bb browser-nrepl relay <-> Extension <-> Page Scittle REPL
```

### Quick Start

1. Start `bb browser-nrepl` (or use VS Code build task)
2. Click **Connect** in Epupp popup (configure ports if needed)
3. Connect REPL client to nREPL port

## REPL Pitfalls

### Navigation Hangs the REPL

On non-SPA sites, setting `js/window.location` (or clicking a link) from a REPL eval can tear down the page and its REPL. The eval response never returns - the connection hangs until the human user cancels the request. Very disruptive!

**Fix: Defer navigation with `setTimeout`:**

```clojure
;; BAD - eval never completes, connection hangs
(set! (.-location js/window) "https://example.com/page")

;; GOOD - returns immediately, navigates after response completes
(js/setTimeout
  #(set! (.-location js/window) "https://example.com/page")
  50)
;; => timeout ID returned instantly
```

After navigation, wait for the new page to load and REPL to reconnect. All prior definitions will be gone - redefine utilities or bake them into a userscript.

### Clipboard Access Blocked

Many sites block `navigator.clipboard.writeText` due to permissions policy. Use a textarea workaround:

```clojure
(defn copy-to-clipboard! [text]
  (let [el (js/document.createElement "textarea")]
    (set! (.-value el) text)
    (.appendChild js/document.body el)
    (.select el)
    (js/document.execCommand "copy")
    (.removeChild js/document.body el)))
```

**Note:** `execCommand("copy")` requires user activation context - it works from a button click handler in a userscript, but returns `false` when called directly from a REPL eval.

### Return Data, Don't Print It

`prn`/`println` output may not be captured by agent tooling. Return values directly:

```clojure
;; Avoid - There is a P in REPL for a reason
(prn result)

;; Prefer - returned as eval result
result
```

## Project Structure

```
my-epupp-hq/
  docs/
    epupp-README.md       # Synced from the Epupp repository
    epupp-repl-fs-sync.md # Also synced from Epupp
  userscripts/           # Userscripts with manifests - ready to sync to Epupp (run `bb tasks` in this directory)
    hq/                  # Example namespace folder
      hello_world.cljs
  live-tampers/          # Ad-hoc REPL code (experiments, repeated patterns)
```

- `docs/` contains documentation synced from the [Epupp repository](https://github.com/PEZ/epupp) — `bb docs-sync` updates from latest available
- `userscripts/` holds scripts intended to be synced to Epupp via FS API or panel
- `live-tampers/` is for REPL-evaluated code - exploratory tampering, utilities, exercises

## What NOT to Do

- **Don't use `epupp/` prefix** in script names - reserved for built-in system scripts
- **Don't assume DOM exists at `document-start`** - `document.body` is null
- **Don't suggest npm packages** - only bundled Scittle libraries are available
- **Don't guess page structure** - evaluate in the REPL to inspect first
- **Don't fight the page's CSS** - work with existing styles, override specifically
